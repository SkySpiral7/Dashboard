import groovy.transform.Field

import java.nio.file.Path
import java.nio.file.Paths

apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.example.e449ps.stormy"
        //lambdas are minSdkVersion 24 also no tag length limit
        minSdkVersion 24
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        mockServices {
            initWith(buildTypes.debug)
        }
    }
    testBuildType "mockServices"

    //needed for robolectric tests
    testOptions.unitTests.includeAndroidResources = true
    dataBinding.enabled = true
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    //OS stuff
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.vectordrawable:vectordrawable-animated:1.1.0'
    implementation 'com.google.android.gms:play-services-location:17.0.0'
    implementation 'com.google.android.material:material:1.1.0'

    //for main
    //TODO: GithubBrowserSample has this great versions.gradle
    def daggerVersion = '2.24'
    def okhttpVersion = '4.1.1'
    def retrofitVersion = '2.8.1'
    implementation "com.google.dagger:dagger:${daggerVersion}"
    annotationProcessor "com.google.dagger:dagger-compiler:${daggerVersion}"
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation "com.squareup.okhttp3:logging-interceptor:${okhttpVersion}"
    implementation "com.squareup.okhttp3:okhttp:${okhttpVersion}"
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.9.0'
    implementation "com.squareup.retrofit2:converter-gson:${retrofitVersion}"
    implementation "com.squareup.retrofit2:retrofit:${retrofitVersion}"
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.19'

    //for all tests
    testImplementation 'junit:junit:4.12'

    //For robolectric tests
    testImplementation 'androidx.test:core:1.2.0'
    testImplementation 'org.robolectric:robolectric:4.3.1'

    //For instrumented tests
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'

    //TODO: need tests first: clean up below dependencies, version, is needed, sort to correct location
    //after tests also see if all deps can update version
    def mockitoVersion = '1.9.5'
    androidTestImplementation "org.mockito:mockito-core:${mockitoVersion}"  //just the version number
    androidTestImplementation 'com.google.dexmaker:dexmaker-mockito:1.2'

    testImplementation "org.mockito:mockito-core:${mockitoVersion}"  //just the version number
    mockServicesImplementation "org.mockito:mockito-core:${mockitoVersion}"  //just the version number
    androidTestAnnotationProcessor "com.google.dagger:dagger-compiler:${daggerVersion}"
    mockServicesAnnotationProcessor "com.google.dagger:dagger-compiler:${daggerVersion}"
    //TODO: is testImplementation mockServicesImplementation 'org.hamcrest:hamcrest-core:1.3' valid?
    testImplementation 'org.hamcrest:hamcrest-core:1.3'
    mockServicesImplementation 'org.hamcrest:hamcrest-core:1.3'
}

//TODO: see if I can move this stuff to the root build.gradle
@Field
private static final String gradlew
if (System.properties['os.name'].toLowerCase().contains('windows')) {
    gradlew = '..\\gradlew.bat'
} else {
    gradlew = '../gradlew'
}
@Field
private static final Path baseReportPath =
        Paths.get(System.getProperty("user.dir"), "build/reports")

/**This will run all tests in androidTest/ folder*/
task runConnectedAndroidTests(type: Exec) {
    createReporterTask(runConnectedAndroidTests, "androidTests/connected")
    commandLine gradlew, 'connectedMockServicesAndroidTest'
}

/**This will run all tests in test/ folder: Robolectric and normal*/
task runJunitTests(type: Exec) {
    createReporterTask(runJunitTests, "tests/testMockServicesUnitTest")
    commandLine gradlew, 'testMockServicesUnitTest'
}

/**This will run all tests in androidTest/ and test/ folders.*/
task runAllTests(dependsOn: ['runConnectedAndroidTests', 'runJunitTests']) {
}

private void createReporterTask(Task previousTask, String reportPath) {
    String taskName = "reporter_" + previousTask.getName()
    previousTask.finalizedBy = [task(taskName) {
        onlyIf {
            previousTask.state.failure != null
        }
        doLast {
            Path indexPath = baseReportPath.resolve(reportPath).resolve("index.html").toAbsolutePath().normalize()
            displayReport(indexPath.toString())
        }
    }]
}

//TODO: is this redundant with newer studio?
private void displayReport(String filePath) {
    if (System.properties['os.name'].toLowerCase().contains('windows')) {
        filePath = filePath.replace("\\", "/")
        filePath = "file:///" + filePath
        exec {
            executable 'explorer'
            args filePath
            ignoreExitValue true
        }
    } else {
        //Jenkins and Windows won't have this variable defined
        //don't run if null or '' because that means there's no UI
        if (System.env.DISPLAY) {
            //TODO: test this on Jenkins and Mac
            filePath = "file://" + filePath
            exec {
                executable 'open'
                args filePath
                ignoreExitValue true
            }
        }
    }
}
